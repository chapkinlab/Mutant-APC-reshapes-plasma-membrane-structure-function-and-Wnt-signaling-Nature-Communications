macro "Lif2Tif Action Tool - C037T0b11LT7b09iTcb09f"
   {
   SOURCE_DIR = getDirectory("Select a Source Directory");
   TARGET_DIR = getDirectory("Select a Target Directory");

   print("Converting lif file in folder: "+SOURCE_DIR);

   // Get all file names
   LIST = getFileList(SOURCE_DIR);
   LIST_ARRAY = newArray(LIST.length);

   // Create extensions array
   for (i=0; i<LIST.length; i++)
      {
      LENGTH = lengthOf(LIST[i]);
      LIST_ARRAY[i] = substring(LIST[i],LENGTH-4,LENGTH);
      }

   setBatchMode(true);

   // Loop on all .lei and .lif extensions
   fileIndex = 0;
   numberOfLifFiles = 0;
   for (n=0; n<LIST.length; n++) 
      {
      if (LIST_ARRAY[n]==".lei" || LIST_ARRAY[n]==".lif") 
      numberOfLifFiles++;
      }
   for (n=0; n<LIST_ARRAY.length; n++)
      {
      if (LIST_ARRAY[n]==".lei" || LIST_ARRAY[n]==".lif")
         {
         fileIndex++;

         // Get the file path
         FILE_PATH = SOURCE_DIR+LIST[n];
         FILE_NAME = File.getName(FILE_PATH);

         // Start BioFormats and get series number in file.
         run("Bio-Formats Macro Extensions");
         Ext.setId(FILE_PATH);
         Ext.getSeriesCount(SERIES_COUNT);
         SERIES_NAMES_ARRAY = newArray(SERIES_COUNT);
         seriesIndex = 0;
         for (i=0; i<SERIES_COUNT; i++) 
            {
            seriesIndex++;
            print("\Converting image " + (fileIndex) + " of " + numberOfLifFiles + " / series " + seriesIndex + " of " + SERIES_COUNT);
            Ext.setSeries(i);
            Ext.getEffectiveSizeC(CHANNEL_COUNT);
            Ext.getSizeZ(SLICE_COUNT);
            run("Bio-Formats Importer", "open=["+ FILE_PATH + "] " + " color_mode=Colorized view=Hyperstack" + " stack_order=Default " + "series_"+d2s(i+1,0));
            title = getTitle();
            rename(title + "-1");
            currentTitle = getTitle();
            index = lastIndexOf(currentTitle, ".lif - ");
            name = substring(currentTitle, index+7, lengthOf(currentTitle)-2);
            save(TARGET_DIR + "/" + FILE_NAME + " - " + name + ".tif");
            close();

            }
         }
      }
      setBatchMode("exit and display");
      showStatus("Finished converting lif(s) to tif(s)");
      print("Finished converting lif(s) to tiff(s)");
   }
